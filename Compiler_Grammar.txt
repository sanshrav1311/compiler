<program> ===> <otherFunctions> <mainFunction>
<mainFunction> ===> TK_MAIN <stmts> TK_END
<otherFunctions> ===> <function><otherFunctions> | ε
<function> ===>TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par> ===>TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> ===>TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR | ε
<parameter_list> ===><dataType> TK_ID <remaining_list>
<remaining_list> ===>TK_COMMA <parameter_list> | ε
<declaration>===> TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM
<dataType> ===> <primitiveDatatype> |<constructedDatatype>
<primitiveDatatype> ===> TK_INT | TK_REAL
<constructedDatatype> ===> TK_RECORD TK_RUID | TK_UNION TK_RUID | TK_RUID
<stmts> ===><typeDefinitions> <declarations> <otherStmts><returnStmt>
<typeDefinitions> ===> <actualOrRedefined> <typeDefinitions> | ε
<actualOrRedefined> ===> <typeDefinition> | <definetypestmt>
<typeDefinition> ===>TK_RECORD TK_RUID <fieldDefinitions> TK_ENDRECORD | TK_UNION TK_RUID  
<fieldDefinitions> ===> <fieldDefinition><fieldDefinition><moreFields>| TK_ENDUNION
<fieldDefinition> ===> TK_TYPE <fieldType> TK_COLON TK_FIELDID TK_SEM
<fieldType> ===> <primitiveDatatype> | <constructedDatatype>
<moreFields> ===><fieldDefinition><moreFields> | ε
<declarations> ===> <declaration><declarations>| ε
<global_or_not> ===> TK_COLON TK_GLOBAL| ε
<iterativeStmt> ===> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt><otherStmts> TK_ENDWHILE
<elsePart> ===>TK_ELSE <stmt><otherStmts> TK_ENDIF | TK_ENDIF
<otherStmts> ===> <stmt><otherStmts> | ε
<stmt> ===> <assignmentStmt> | <iterativeStmt>|<conditionalStmt>|<ioStmt>| <funCallStmt>
<conditionalStmt> ===> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt><otherStmts> <elsePart>
<assignmentStmt> ===><SingleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
<ioStmt> ===>TK_READ TK_OP <var> TK_CL TK_SEM | TK_WRITE TK_OP <var> TK_CL TK_SEM
<term> ===> <factor> <termPrime>
<termPrime> ===> <highPrecedenceOperators><factor> <termPrime> | ε
<factor> ===> TK_OP <arithmeticExpression> TK_CL | <var>
<var>===> <SingleOrRecId> | TK_NUM | TK_RNUM
<arithmeticExpression> ===> <term> <expPrime>
<expPrime> ===> <lowPrecedenceOperators> <term> <expPrime> | ε
<SingleOrRecId> ===>TK_ID <option_single_constructed>
<option_single_constructed> ===> ε | <oneExpansion><moreExpansions>
<oneExpansion> ===> TK_DOT TK_FIELDID 
<moreExpansions> ===> <oneExpansion> <moreExpansions> | ε
<funCallStmt> ===><outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters> ===> TK_SQL <idList> TK_SQR TK_ASSIGNOP | ε
<inputParameters> ===> TK_SQL <idList> TK_SQR
<highPrecedenceOperators> ===> TK_MUL | TK_DIV
<lowPrecedenceOperators> ===> TK_PLUS | TK_MINUS
<booleanExpression> ===>TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL | <var> <relationalOp> <var> | TK_NOT TK_OP <booleanExpression> TK_CL
<logicalOp> ===>TK_AND | TK_OR
<relationalOp> ===> TK_LT | TK_LE | TK_EQ | TK_GT | TK_GE | TK_NE
<returnStmt> ===>TK_RETURN <optionalReturn> TK_SEM
<optionalReturn> ===>TK_SQL <idList> TK_SQR | ε
<idList> ===> TK_ID <more_ids>
<more_ids> ===> TK_COMMA <idList> | ε
<definetypestmt> ===>TK_DEFINETYPE <A> TK_RUID TK_AS TK_RUID
<A> ===> TK_RECORD | TK_UNION